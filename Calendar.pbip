Calendar =
VAR DateStart =
    DATE ( 2015, 1, 1 ) -- Calendar Start Date. Change this as per your requirement.
VAR DateEnd =
    DATE ( 2025, 12, 31 ) -- Calendar End Date.
VAR CalTab =
    ADDCOLUMNS (
        CALENDAR ( DateStart, DateEnd ),
        "DateKey", FORMAT ( [Date], "YYYYMMDD" ),
        "Year", YEAR ( [Date] ),
        "MonthNumber", FORMAT ( [Date], "MM" ),
        "YearMonthNumber", FORMAT ( [Date], "YYYY-MM" ),
        "YearMonthShort", FORMAT ( [Date], "YYYY-mmm" ),
        "MonthNameShort", FORMAT ( [Date], "mmm" ),
        "MonthName", FORMAT ( [Date], "mmmm" ),
        "IsWeekDay", IF ( WEEKDAY ( [Date] ) = 7, "No", IF ( WEEKDAY ( [Date] ) = 1, "No", "Yes" ) ),
        "DayOfWeekNumber", WEEKDAY ( [Date] ),
        "DayOfWeek", FORMAT ( [Date], "dddd" ),
        "DayOfWeekShort", FORMAT ( [Date], "ddd" ),
        "Quarter", "Q" & FORMAT ( [Date], "Q" ),
        "YearQuarter", FORMAT ( [Date], "YYYY" ) & "-Q" & FORMAT ( [Date], "Q" ),
        "FiscalYear", IF ( MONTH ( [Date] ) >= 4, YEAR ( [Date] ), YEAR ( [Date] ) - 1 ),
        "FiscalMonthNumber", IF ( MONTH ( [Date] ) >= 4, MONTH ( [Date] ) - 3, MONTH ( [Date] ) + 9 ),
        "FiscalQuarter", 
            SWITCH(
                TRUE(),
                MONTH ( [Date] ) IN {4, 5, 6}, "Q1",
                MONTH ( [Date] ) IN {7, 8, 9}, "Q2",
                MONTH ( [Date] ) IN {10, 11, 12}, "Q3",
                "Q4"
            ),
        "FiscalYearQuarter", 
            FORMAT ( IF ( MONTH ( [Date] ) >= 4, YEAR ( [Date] ), YEAR ( [Date] ) - 1 ), "YYYY" ) & "-Q" & 
            SWITCH(
                TRUE(),
                MONTH ( [Date] ) IN {4, 5, 6}, "1",
                MONTH ( [Date] ) IN {7, 8, 9}, "2",
                MONTH ( [Date] ) IN {10, 11, 12}, "3",
                "4"
            )
    )
RETURN
    CalTab
